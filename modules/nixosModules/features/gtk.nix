{
  flake.nixosModules.gtk = {
    pkgs,
    lib,
    ...
  }: let
    inherit (lib) mkDefault;

    theme-name = "Gruvbox-Green-Dark-Medium";
    theme-package = pkgs.gruvbox-gtk-theme.override {
      colorVariants = ["dark"];
      sizeVariants = ["standard"];
      themeVariants = ["green"];
      tweakVariants = ["medium" "macos"];
    };

    icon-theme-package = pkgs.gruvbox-plus-icons;
    icon-theme-name = "Gruvbox-Plus-Dark";

    cursor-theme-package = pkgs.bibata-cursors;
    cursor-theme-name = "Bibata-Modern-Classic";
    cursor-size = 24;

    gtksettings = ''
      [Settings]
      gtk-icon-theme-name = ${icon-theme-name}
      gtk-theme-name = ${theme-name}
      gtk-cursor-theme-name = ${cursor-theme-name}
      gtk-cursor-theme-size = ${toString cursor-size}
    '';
  in {
    environment = {
      etc = {
        "xdg/gtk-3.0/settings.ini".text = gtksettings;
        "xdg/gtk-4.0/settings.ini".text = gtksettings;
      };
    };

    environment.variables = {
      GTK_THEME = theme-name;
      XCURSOR_THEME = cursor-theme-name;
      XCURSOR_SIZE = toString cursor-size;
    };

    programs = {
      dconf = {
        enable = mkDefault true;
        profiles = {
          user = {
            databases = [
              {
                lockAll = false;
                settings = {
                  "org/gnome/desktop/interface" = {
                    gtk-theme = theme-name;
                    icon-theme = icon-theme-name;
                    color-scheme = "prefer-dark";
                    cursor-theme = cursor-theme-name;
                    cursor-size = lib.gvariant.mkInt32 cursor-size;
                  };
                };
              }
            ];
          };
        };
      };
    };

    environment.systemPackages = [
      theme-package
      icon-theme-package
      cursor-theme-package

      pkgs.gtk3
      pkgs.gtk4
    ];
  };
}
